from pathlib import Path
from snakemake.utils import min_version

min_version("6.0")

configfile: "config/config.yaml"
configfile: "config/targets.yaml"


def iter_lines(file_path):
    with open(file_path) as f:
        for line in f:
            yield line.strip()


def iter_files_in_path(path: Path):
    """Iterates over non-hidden files"""
    for file in path.glob("*"):
        if not file.name.startswith("."):
            yield file


def iter_samples_in_path(path: Path):
    for file in iter_files_in_path(path):
        yield file.name.split(".")[0]


# Targets
## Output
OUTPUT_NAME = config["OUTPUT_NAME"]
OUTDIR = Path(config["OUTPUT_DIRECTORY"])
OUTDIR.mkdir(parents=True, exist_ok=True)
## Data folders
BAM_FOLDER = Path(config["BAM_FOLDER"])
FASTA_FOLDER = Path(config["FASTA_FOLDER"])
## Metadata table
METADATA = Path(config["METADATA"])


# Tree parameters
ETC_TREE_PARAMS = []
## UFBoot
if config["N_UFBOOT"] != 0:
    ETC_TREE_PARAMS.append(f"-B {config['N_UFBOOT']:d}")
ETC_TREE_PARAMS = " ".join(ETC_TREE_PARAMS)


# Rules
include: "rules/fetch.smk"
include: "rules/fasta.smk"
include: "rules/asr.smk"
include: "rules/demix.smk"
include: "rules/vaf.smk"
include: "rules/pangolin.smk"
include: "rules/distances.smk"
include: "rules/evolution.smk"


rule all:
    input:
        OUTDIR/f"{OUTPUT_NAME}.ancestor.fasta",
        expand(OUTDIR/"demixing"/"{sample}", sample=iter_samples_in_path(BAM_FOLDER)),
        OUTDIR/"summary_freyja_demixing.csv",
        OUTDIR/f"{OUTPUT_NAME}.masked.filtered.tsv",
        OUTDIR/f"{OUTPUT_NAME}.lineage_report.csv",
        OUTDIR/f"{OUTPUT_NAME}.weighted_distances.csv",
        OUTDIR/f"{OUTPUT_NAME}.ancestor.N_S.sites.csv"

